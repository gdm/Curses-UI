#!/usr/bin/perl -w

use strict;

use FindBin;
use lib "$FindBin::RealBin/../lib";
use Curses::UI;
use Curses; # For KEY_*()

use Net::POP3;
use Mail::Header;
use Mail::Address;

my $cui = new Curses::UI ( -clear_on_exit => 1 );
my $pop3 = undef;
my $connection = undef;

# We do not want STDERR to clutter our screen.
open STDERR, ">/dev/null";

# ----------------------------------------------------------------------
# setup(): Setup the connection
# ----------------------------------------------------------------------

sub setup_connection()
{

	my $conwin = $cui->add(
		'connection_window', 'Window',
		-border => 1,
		-ipad => 2,
		-height => 15,
		-width => 60,
		-centered => 1,
		-title => "POP3 connection",
	);	 

	$conwin->add(
		'host_label', 'Label',	
		-x => 0, -y => 0, -width => 13,
		-textalignment => 'right',
		-text => 'POP3 server :',
	);

	$conwin->add(
		'host', 'TextEntry',
		-x => 14, -y => 0,
		-text => 'pop',
	);

	$conwin->add(
		'port_label', 'Label',	
		-x => 0, -y => 2, -width => 13,
		-textalignment => 'right',
		-text => 'POP3 port :',
	);

	$conwin->add(
		'port', 'TextEntry',
		-x => 14, -y => 2,
		-regexp => '/^\d*$/',	
		-text => '110',
	);

	$conwin->add(
		'username_label', 'Label',	
		-x => 0, -y => 4, -width => 13,
		-textalignment => 'right',
		-text => 'Username :',
	);

	$conwin->add(
		'username', 'TextEntry',
		-x => 14, -y => 4,
		-text => getpwuid($>),
	);

	$conwin->add(
		'password_label', 'Label',	
		-x => 0, -y => 6, -width => 13,
		-textalignment => 'right',
		-text => 'Password :',
	);

	$conwin->add(
		'password', 'TextEntry',
		-x => 14, -y => 6,
		-password => '*',
		-text => '',
	);

	my $buttons = $conwin->add(
		'buttons', 'ButtonBox',
		-x => 14, -y => 8,
		-buttons => [
		    { -label => '< Connect >',
		      -value => 'connect' },
		    { -label => '< Quit >',
		      -value => 'quit' },
		],
	);


	$conwin->returnkeys("\cC");
	$conwin->draw;

	my %connection = ();
	for(;;)
	{
		my ($return, $lastkey) = $conwin->focus;

		# See if Connect or Quit was chosen from the menu.
		my $btnval = $conwin->getobj('buttons')->get;

		# Quit if CTRL+C was pressed or if Quit was pressed.
		exit_program() if $lastkey eq "\cC" or $btnval eq 'quit';

		# Get the connection data from the connection window.
		my $error = 0;
		foreach my $key ('username','password','host','port') {
			my $value = $conwin->getobj($key)->get;
			$connection{$key} = $value;
			if ($value =~ /^\s*$/) {
				$cui->error("Missing value for $key field");
				$error = 1;
			}
		}

		last unless $error;
	}

	# We do not need the connection window anymore. Get rid of it.
	$cui->delete('connection_window');
	$cui->draw;
	
	$connection = \%connection;
}


# ----------------------------------------------------------------------
# pop3_connect(): Connect to the POP3 server and exit if it fails
# ----------------------------------------------------------------------

sub pop3_connect()
{
	$cui->status("Connecting to the POP3 server...");

	$pop3 = Net::POP3->new(
		$connection->{host},
		Port => $connection->{port},
		Timeout => 0,
	);

	if (not $pop3) {
		$cui->error("Could not connect to "
		           ."$connection->{host}:$connection->{port}");
		exit(1);
	}

	$cui->status("Sending username...");
	if (not defined $pop3->user($connection->{username})) {
		$cui->error("Sending USER failed:\n"
			   . $pop3->message() );
		exit(1);
	}
	
	$cui->status("Sending password...");
	if (not defined $pop3->pass($connection->{password})) {
		$cui->error("Sending PASS failed:\n"
			   . $pop3->message() );
		exit(1);
	}

	$cui->nostatus;

	return $pop3;
}

# ----------------------------------------------------------------------
# inbox(): Show the contents of the inbox
# ----------------------------------------------------------------------

sub inbox()
{
	$cui->status("Retrieving message list...");

	my $list = $pop3->list();
	my @ids = sort {$a<=>$b} keys %$list;

	my $msg = "Retrieving headers";
	$cui->progress(
		-max => scalar(@ids),
		-message => $msg,
	);

	my @values = ();
	my %labels = ();
	my $progress_pos = 0;
	foreach my $n (@ids)
	{
		my $lines = $pop3->top($n, 0);
		my $header = new Mail::Header($lines);

		# Add value
		push @values, $n;

		# Add label
		my $subject = $header->get('Subject');
		my $from    = $header->get('From');
	 	my $addr = new Mail::Address($from);	
		my $name = substr($addr->name, 0, 15);
		$labels{$n} = sprintf("%4d", $n)
			    . " | "
			    . sprintf("%15s", $name)
			    . " | "
                            . $header->get('Subject');
	
		$cui->setprogress(
			++$progress_pos,
			$msg . ": message $progress_pos of " . scalar(@ids)
		);
	}
	$cui->noprogress;

	my $listwin = $cui->add('list_window', 'Window');
	my $ml = $listwin->add(
		'message_list', 'ListBox',
		-values => \@values,
		-labels => \%labels,
		-vscrollbar => 1,
		-border => 1,
		-ipad   => 1,
		-title  => '<ENTER> view message <CTR+Q> Quit from program',
	);
	
	$cui->nostatus;

	$ml->set_routine('option-select', \&view_message);
	$listwin->returnkeys("\cQ", "\cC");
	$listwin->focus;
}

# ----------------------------------------------------------------------
# view_message(): callback routine for the inbox list
# ----------------------------------------------------------------------

sub view_message()
{
	my $this = shift;	

	# Get the selected message id.
	$this->{-selected} = $this->{-ypos};
	my $id = $this->get;
	$this->{-selected} = undef;
	
	# Retrieve the message from the POP3 server.
	$cui->status("Retrieving message $id from the POP3 server...");
	my $lines = $pop3->get($id);		
	unless (ref $lines)
	{
		# Maybe the connection went away. Reconnect and try again.
		$pop3->close;
		pop3_connect();
		$lines = $pop3->get($id);		
	}
	unless (ref $lines)
	{
		$cui->error("Failed to retrieve message $id\n"
		           ."from the POP3 server.\n"
                           ."Even after reconnecting");
		exit_program();
	}
	$cui->nostatus;

	# Create the viewer window.
	my $viewwin = $cui->add('view_window', 'Window');
	my $tv = $viewwin->add(
		'textviewer', 'TextViewer',
		-text => join("", @$lines),
		-vscrollbar => 1,
		-wrapping => 1,
		-border => 1,
		-ipad   => 1,
		-title  => '<ENTER> return to inbox <CTRL+Q>: Quit from program',
	);

	$viewwin->returnkeys(KEY_ENTER(), "\cQ", "\cC");
	$cui->draw;

	my (undef, $key) = $viewwin->focus;
	exit_program() if $key eq "\cQ" or $key eq "\cC";
	
	$cui->delete('view_window');
	$cui->draw;
	
	return $this;
}

# ----------------------------------------------------------------------
# exit_program(): clean exit
# ----------------------------------------------------------------------

sub exit_program()
{
	if (defined $pop3)
	{
		$cui->status("Closing POP3 connection...");
		$pop3->quit;
		$cui->nostatus;
	}
	exit;
}

# ----------------------------------------------------------------------
# Mainloop
# ----------------------------------------------------------------------

setup_connection();
pop3_connect();
inbox();
